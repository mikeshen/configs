"
"
"                        __   __(_)_ __ ___
"                        \ \ / /| | '_ ` _ \
"                        |\ V / | | | | | | |
"                          \_/  |_|_| |_| |_|
"
"
"Begin dein Scripts-------------------------------------------------------- {
    set nocompatible                                  " Be iMproved

    let s:config_dir = $HOME . '/Development/configs'
    let s:cache_dir = expand(s:config_dir . '/vim/cache')
    set runtimepath+=/home/osboxes/Development/configs/vim/repos/github.com/Shougo/dein.vim
    set rtp+=~/.fzf
    call dein#begin(expand(s:config_dir . '/vim'))
    call dein#add('Shougo/dein.vim') " let dein manage dein

    " ADD OR REMOVE PLUGINS HERE

    " 1. Utilities {
    call dein#add('tpope/vim-dispatch')               " execute make commands within vim
    call dein#add('tpope/vim-eunuch')                 " vim sugar for UNIX plugins
    call dein#add('xolox/vim-misc')                   " autoload vimscripts, needed for vim session
    call dein#add('Shougo/unite.vim')                 " needed for codequery to work
    call dein#add('xolox/vim-session')                " extended better session management
    call dein#add('vim-utils/vim-man')                " vim man
    call dein#add('jmcantrell/vim-virtualenv')        " work with python virtualenv
    call dein#add('jpalardy/vim-slime')               " send text over tmux in repl-like way
    call dein#add('vimwiki/vimwiki')                  " your personal notes / todos / markdown editor vim
    call dein#add('chrisbra/csv.vim')                 " quickly edit and format csv files
    call dein#add('vim-scripts/conque-GDB')           " your own GDB interface emulator in vim
    call dein#add('mileszs/ack.vim')                  " smart and happy searching
    " }

    " 2. UI / Themes / Color {
    call dein#add('flazz/vim-colorschemes')           " hundreds of colorschemes
    call dein#add('vim-airline/vim-airline')          " vim-airline powerbar
    call dein#add('vim-airline/vim-airline-themes')   " themes for vim-airline
    call dein#add('ryanoasis/vim-devicons')            " icons for stuff
    call dein#add('edkolev/tmuxline.vim')             " tmux integration with vim-airline
    call dein#add('Yggdroot/indentLine')              " indent guides for various languages
    call dein#add('kien/rainbow_parentheses.vim')     " differing colors for stacked parentheses
    call dein#add('okcompute/vim-python-match')       " redefines % motion soo that it cycles through python control statements correctly
    call dein#add('Valloric/MatchTagAlways')          " highlight enclosing xml / html tags
    call dein#add('elzr/vim-json')
    " }

    " 3. Filesystem Navigation / Movement {
    call dein#add('ctrlpvim/ctrlp.vim')               " fast file finder
    call dein#add('scrooloose/nerdtree')              " display a filesystem browser
    call dein#add('junegunn/fzf.vim')                 " fzf vim plugin
    call dein#add('devjoe/vim-codequery')             " search + browse code more efficiently
    call dein#add('christoomey/vim-tmux-navigator')   " jump between tmux panes and vim splits seamlessly
    call dein#add('derekwyatt/vim-fswitch')           " swtich between .h and .cpp files at the press of a key
    call dein#add('bogado/file-line')                 " file:line format when opening files
    call dein#add('majutsushi/tagbar')                " displays tags in a window, ordered by scope
    call dein#add('artnez/vim-wipeout')               " quickly remove all unopened buffers with :Wipeout
    call dein#add('qpkorr/vim-bufkill')               " kill buffers without destroying panes
    call dein#add('easymotion/vim-easymotion')        " easily jump around using labeled tags
    call dein#add('tpope/vim-unimpaired')             " extremely useful [] bracket shortcuts
    call dein#add('rhysd/clever-f.vim')               " smartly repeat f and F searches using the same keys
    call dein#add('tmhedberg/matchit')                " extend % to work for tags
    " }

    " 4. Editing {
    call dein#add('tpope/vim-commentary')             " minimimilistic commenting shortcuts
    call dein#add('tpope/vim-surround')               " make quoting/tags replacement easier
    call dein#add('tpope/vim-speeddating')            " easy increment dates with c-a, c-x
    call dein#add('tpope/vim-repeat')                 " allow vim . repeasts for custom plugins
    call dein#add('terryma/vim-expand-region')        " quickly select alrger regions of text using same key combo
    call dein#add('godlygeek/tabular')                " better editing for simple alignments
    call dein#add('sjl/gundo.vim')                    " undo tree visualized
    call dein#add('junegunn/goyo.vim')                " distraction free editing
    call dein#add('Chiel92/vim-autoformat')           " quickly format code in-line
    " call dein#add('Raimondi/delimitMate')             " automatic closing of quotes, parents, brackets
    " }

    " 5. Version Control Integration {
    call dein#add('tpope/vim-fugitive')               " GIT integration
    call dein#add('airblade/vim-gitgutter')           " display git information in the gutter
    call dein#add('albfan/vim-timelapse')             " vim time differences between commits
    call dein#add('chrisbra/vim-diff-enhanced')       " show different vim diff options
    call dein#add('AndrewRadev/linediff.vim')         " perform difs on lines of code
    " }

    " 6. Snippets {
    call dein#add('Shougo/deoplete.nvim')             " completeion engine
    call dein#add('zchee/deoplete-clang')             " clang completion
    call dein#add('SirVer/ultisnips')                 " snippet engine
    call dein#add('honza/vim-snippets')               " snippets repo
    " }

    " 7. NeoVim {
    " }

    call dein#end()

    " If you want to install not installed plugins on startup.
    if dein#check_install()
      call dein#install()
    endif

"End dein Scripts-------------------------------------------------------- }

" Environment {

    " Identify platform {
        silent function! OSX()
            return has('macunix')
        endfunction
        silent function! LINUX()
            return has('unix') && !has('macunix') && !has('win32unix')
        endfunction
        silent function! WINDOWS()
            return  (has('win32') || has('win64'))
        endfunction
    " }

    " Basics {
        if !WINDOWS()
            set shell=/bin/zsh
        endif
    " }

" }

" General {

    set background=dark         " Assume a dark background
    colorscheme gruvbox

    " FUNCTIONS

    " Allow to trigger background
    function! ToggleBG()
        let s:tbg = &background
        " Inversion
        if s:tbg == "dark"
            set background=light
        else
            set background=dark
        endif
    endfunction

    " Map key to toggle opt
    function! MapToggle(key, opt)
        let cmd = ':set '.a:opt.'! \| set '.a:opt."?\<CR>"
        exec 'nnoremap '.a:key.' '.cmd
        exec 'inoremap '.a:key." \<C-O>".cmd
    endfunction
    command! -nargs=+ MapToggle call MapToggle(<f-args>)

    filetype plugin indent on   " Automatically detect file types.
    syntax on                   " Syntax highlighting
    " set mouse=a                 " Automatically enable mouse usage
    set mousehide               " Hide the mouse cursor while typing
    scriptencoding utf-8

    set clipboard=unnamedplus

    " Most prefer to automatically switch to the current file directory when
    " a new buffer is opened
    autocmd BufEnter * if bufname("") !~ "^\[A-Za-z0-9\]*://" | lcd %:p:h | endif

    " set autowrite                       " Automatically write a file when leaving a modified buffer
    set shortmess+=aTItO                " Abbrev. of messages (avoids 'hit enter')
    set viewoptions=folds,options,cursor,unix,slash " Better Unix / Windows compatibility
    set virtualedit=onemore             " Allow for cursor beyond last character
    set history=1000                    " Store a ton of history (default is 20)
    " set spell                           " Spell checking on
    set hidden                          " Allow buffer switching without saving
    set iskeyword-=.                    " '.' is an end of word designator
    set iskeyword-=#                    " '#' is an end of word designator
    set iskeyword-=-                    " '-' is an end of word designator
    set timeoutlen=500                  " time to give vim before key timeout
    set ttimeoutlen=0

    " Instead of reverting the cursor to the last position in the buffer, we
    " set it to the first line when editing a git commit message
    au FileType gitcommit au! BufEnter COMMIT_EDITMSG call setpos('.', [0, 1, 1, 0])

    " http://vim.wikia.com/wiki/Restore_cursor_to_file_position_in_previous_editing_session
    " Restore cursor to file position in previous editing session
    function! ResCur()
        if line("'\"") <= line("$")
            silent! normal! g`"
            return 1
        endif
    endfunction

    augroup resCur
        autocmd!
        autocmd BufWinEnter * call ResCur()
    augroup END

    " Setting up the directories {
        set backup                  " Backups are nice ...
        if has('persistent_undo')
            set undofile                " So is persistent undo ...
            set undolevels=1000         " Maximum number of changes that can be undone
            set undoreload=10000        " Maximum number lines to save for undo on a buffer reload
        endif
    " }

" }

" Vim UI {

    set tabpagemax=15               " Only show 15 tabs
    set showmode                    " Display the current mode
    set cursorline                  " Highlight current line
    set noea                        " Don't resize all buffers when a split is closed

    highlight clear SignColumn      " SignColumn should match background
    highlight clear LineNr          " Current line number row will have same background color in relative mode
    "highlight clear CursorLineNr    " Remove highlight color from current line number

    if has('cmdline_info')
        set ruler                   " Show the ruler
        set rulerformat=%30(%=\:b%n%y%m%r%w\ %l,%c%V\ %P%) " A ruler on steroids
        set showcmd                 " Show partial commands in status line and
                                    " Selected characters/lines in visual mode
    endif

    if has('statusline')
        set laststatus=2

        " Broken down into easily includeable segments
        set statusline=%<%f\                     " Filename
        set statusline+=%w%h%m%r                 " Options
        " if !exists('g:override_spf13_bundles')
            " set statusline+=%{fugitive#statusline()} " Git Hotness
        " endif
        set statusline+=\ [%{&ff}/%Y]            " Filetype
        set statusline+=\ [%{getcwd()}]          " Current dir
        set statusline+=%=%-14.(%l,%c%V%)\ %p%%  " Right aligned file nav info
    endif

    set backspace=indent,eol,start  " Backspace for dummies
    set linespace=0                 " No extra spaces between rows
    set number                      " Line numbers on
    set relativenumber              " relative numberline, nice when combined with number
    set showmatch                   " Show matching brackets/parenthesis
    set incsearch                   " Find as you type search
    set hlsearch                    " Highlight search terms
    " "set matchpairs+=<:>             " Match, to be used with %
    set winminheight=0              " Windows can be 0 line high
    set ignorecase                  " Case insensitive search
    set smartcase                   " Case sensitive when uc present
    set wildmenu                    " Show list instead of just completing
    set wildmode=list:longest,full  " Command <Tab> completion, list matches, then longest common part, then all.
    set whichwrap=b,s,h,l,<,>,[,]   " Backspace and cursor keys wrap too
    set scrolljump=5                " Lines to scroll when cursor leaves screen
    set scrolloff=3                 " Minimum lines to keep above and below cursor
    set foldenable                  " Auto fold code
    set list
    set listchars=tab:›\ ,trail:•,extends:#,nbsp:. " Highlight problematic whitespace

" }

" Formatting {

    set nowrap                      " Do not wrap long lines
    set autoindent                  " Indent at the same level of the previous line
    set shiftwidth=4                " Use indents of 4 spaces
    set expandtab                   " Tabs are spaces, not tabs
    set tabstop=4                   " An indentation every four columns
    set softtabstop=4               " Let backspace delete indent
    set nojoinspaces                " Prevents inserting two spaces after punctuation on a join (J)
    set splitright                  " Puts new vsplit windows to the right of the current
    set splitbelow                  " Puts new split windows to the bottom of the current
    set pastetoggle=<F12>           " pastetoggle (sane indentation on pastes)
    "set comments=sl:/*,mb:*,elx:*/  " auto format comment blocks

    "autocmd FileType go autocmd BufWritePre <buffer> Fmt
    autocmd BufNewFile,BufRead *.html.twig set filetype=html.twig
    autocmd FileType haskell,puppet,ruby,yml setlocal expandtab shiftwidth=2 softtabstop=2

    autocmd BufNewFile,BufRead *.coffee set filetype=coffee

    autocmd FileType * setlocal formatoptions-=c formatoptions-=r formatoptions-=o

    " Workaround vim-commentary for Haskell
    autocmd FileType haskell setlocal commentstring=--\ %s
    " Workaround broken colour highlighting in Haskell
    autocmd FileType haskell,rust setlocal nospell

" }

" Key (re)Mappings {

    let mapleader = ' '
    let maplocalleader = ';'

    " Easier moving in tabs and windows
    " The lines conflict with the default digraph mapping of <C-K>
    map <C-j> <C-W>j<C-W>
    map <C-k> <C-W>k<C-W>
    map <C-l> <C-W>l<C-W>
    map <C-h> <C-W>h<C-W>

    " Wrapped lines goes down/up to next row, rather than next line in file.
    noremap j gj
    noremap k gk

    " End/Start of line motion keys act relative to row/wrap width in the
    " presence of `:set wrap`, and relative to line for `:set nowrap`.
    " Default vim behaviour is to act relative to text line in both cases
    " If you prefer the default behaviour, add the following to your
    function! WrapRelativeMotion(key, ...)
        let vis_sel=""
        if a:0
            let vis_sel="gv"
        endif
        if &wrap
            execute "normal!" vis_sel . "g" . a:key
        else
            execute "normal!" vis_sel . a:key
        endif
    endfunction

    " Map g* keys in Normal, Operator-pending, and Visual+select
    noremap $ :call WrapRelativeMotion("$")<cr>
    noremap <End> :call WrapRelativeMotion("$")<cr>
    noremap 0 :call WrapRelativeMotion("0")<cr>
    noremap <Home> :call WrapRelativeMotion("0")<cr>
    noremap ^ :call WrapRelativeMotion("^")<cr>
    " Overwrite the operator pending $/<End> mappings from above
    " to force inclusive motion with :execute normal!
    onoremap $ v:call WrapRelativeMotion("$")<cr>
    onoremap <End> v:call WrapRelativeMotion("$")<cr>
    " Overwrite the Visual+select mode mappings from above
    " to ensure the correct vis_sel flag is passed to function
    vnoremap $ :<C-U>call WrapRelativeMotion("$", 1)<cr>
    vnoremap <End> :<C-U>call WrapRelativeMotion("$", 1)<cr>
    vnoremap 0 :<C-U>call WrapRelativeMotion("0", 1)<cr>
    vnoremap <Home> :<C-U>call WrapRelativeMotion("0", 1)<cr>
    vnoremap ^ :<C-U>call WrapRelativeMotion("^", 1)<cr>

    " Stupid shift key fixes
    if has("user_commands")
        command! -bang -nargs=* -complete=file E e<bang> <args>
        command! -bang -nargs=* -complete=file W w<bang> <args>
        command! -bang -nargs=* -complete=file Wq wq<bang> <args>
        command! -bang -nargs=* -complete=file WQ wq<bang> <args>
        command! -bang Wa wa<bang>
        command! -bang WA wa<bang>
        command! -bang Q q<bang>
        command! -bang QA qa<bang>
        command! -bang Qa qa<bang>
        command! -bang WQA wqa<bang>
        command! -bang WQA wqa<bang>
    endif

    cmap Tabe tabe

    " Yank from the cursor to the end of the line, to be consistent with C and D.
    nnoremap Y y$
    nnoremap <silent> <leader>/ :set invhlsearch<cr>
    " Change Working Directory to that of the current file
    cmap cwd lcd %:p:h
    cmap cd. lcd %:p:h
    " Visual shifting (does not exit Visual mode)
    vnoremap < <gv
    vnoremap > >gv
    " Allow using the repeat operator with a visual selection (!)
    " http://stackoverflow.com/a/8064607/127816
    vnoremap . :normal .<cr>
    " For when you forget to sudo.. Really Write the file.
    cmap w!! w !sudo tee % >/dev/null
    " Adjust viewports to the same size
    noremap <Leader>= <C-w>=
    " Map <Leader>ff to display all lines with keyword under cursor
    " and ask which one to jump to
    nnoremap <Leader>ff [I:let nr = input("Which one: ")<Bar>exe "normal " . nr ."[\t"<cr>
    " Easier horizontal scrolling
    nnoremap zl zL
    nnoremap zh zH

    " USER KEYBINDING CHANGES {

        " insert mode quick delete line
        inoremap <C-d> <esc>ddi

        " fast commands
        map <leader>r :
        " Toggle numbercolumn
        noremap <leader>n :set invnumber<cr> :set invrelativenumber<cr>
        " strip output of a file
        nnoremap <localleader>st :call StripTrailingWhitespace()<cr>
        " change cdpath to current file
        nnoremap <localleader>c :lcd %:p:h<cr>
        " fast escape with jk and kj
        inoremap jk <esc>l
        inoremap kj <esc>l
        inoremap JK <esc>l
        inoremap jK <esc>l
        inoremap Jk <esc>l
        inoremap KJ <esc>l
        inoremap kJ <esc>l
        inoremap Kj <esc>l
        " ghetto bracket completion
        inoremap { {}<left> " }
        " easier record mode playback for the q register
        noremap Q @q
        "HJKL Resizing
        noremap J :resize +8<cr>
        noremap K :resize -8<cr>
        noremap H :vertical resize -8<cr>
        noremap L :vertical resize +8<cr>
        " fast saving + quit
        noremap <leader>w :w!<cr>
        noremap <leader>q :q<cr>
	" fast jump between last two open buffers
	nnoremap <bs> <C-^>
        " fast Make
        nnoremap <leader>m :Make make<cr>
        " fast diff put
        nmap <F6> :diffg<cr>
        " fast search/replace
        nnoremap <localleader>// q:i%s//g<esc>ba
        nnoremap <localleader>/c q:i%s//gc<esc>bba
        " fast source vimrc
        nnoremap <leader><leader>v :source ~/.vimrc<cr>
        " fast splits
        noremap <leader>s :split<cr>
        noremap <leader>v :vsplit<cr>
        " fast q:
        noremap <leader>; q:
        " fast wrap/nowrap toggle
        MapToggle <F4> wrap
        " add split maxing / fullscreen mode
        noremap<leader>z :tab split<cr>
        " change through buffers with <tab> <Shift-tab>
        noremap <tab> :bnext<cr>
        noremap <S-tab> :bprevious<cr>
        noremap <localleader>f :bnext<cr>
        noremap <localleader>d :bprevious<cr>
        " easier tab opening and closing
        nnoremap <leader>tn :tabnew<cr>
        nnoremap <leader>to :tabnew<cr>
        nnoremap <leader>tc :tabclose<cr>
        nnoremap <leader>tm :tabmove<cr>
        nnoremap <localleader>j gT
        nnoremap <localleader>k gt
        " Speed up scrolling of the viewport slightly
        noremap <C-e> 2<C-e>
        noremap <C-y> 2<C-y>
    " }

    " KEYBOARD SHORTENINGS {

        " ;; logging shortcuts
        nnoremap <localleader>;c ostd::cout << "" << std::endl;<esc>BBBa
        nnoremap <localleader>;ii o#include ""<esc>i
        nnoremap <localleader>;io o#include <><esc>
        nnoremap <localleader>;/ OMikeComment: <esc>:Commentary<cr>A
        nnoremap <localleader>;a oMikeComment: <esc>:Commentary<cr>kJA
        nnoremap <localleader>;t OTODO: <esc>:Commentary<cr>A

    " }

" }

" Plugins {

    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
    " 1 Utilities
    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

    " vim-session {
        let g:session_autosave = 'no'
        let g:session_autoload = 'no'
        let g:session_persist_font = 0
        let g:session_persist_colors = 0
        nnoremap <localleader>sd :DeleteSession! 
        nnoremap <localleader>ss :SaveSession! 
        nnoremap <localleader>so :OpenSession! 
    " }

    " vim-wiki {
        nnoremap <localleader>s :VimwikiToggleListItem<cr>
        nnoremap <localleader>ll d:VimwikiDiaryGenerateLinks<cr>
        nnoremap <F5> "=strftime("%b %d, %Y")<cr>P
        inoremap <F5> <c-r>=strftime("%b %d, %Y")<cr>
    " }

    " Ack/Ag THE SILVER SEARCHER {
        if executable('ag')
            let g:ackprg = 'ag --vimgrep'
        endif

        " search shortcuts
        " under word
        noremap <localleader>a :Ack! --ignore={.git,.svn} -SRi -w <C-r><C-w><cr>
        " in current pwd directory
        noremap <localleader>gg q:iAck! --cpp --ignore={.git,.svn} -SRi <esc>lxi
    " }

    " Ctags {
        set tags=./tags;/,~/.vimtags

        " ctags prompt behavior
        noremap <C-]> g<C-]>
    " }

    " vim-slime {
        let g:slime_target = 'tmux'
        let g:slime_default_config = {"socket_name": split($TMUX, ",")[0], "target_pane": ":.1"}
    " }

    " conque-gdb {
        let g:ConqueGdb_Leader = '\'
    " }

    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
    " 2 UI / Themes / Color
    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

    " vim-colorscheme {
      if !has('gui_running')
        set t_Co=256      " set terminal colors to 256
        set t_ut=         " use the background color of the terminal
        if &termencoding == ""
          let &termencoding = &encoding
        endif
      endif
    " }

    " vim-airline / vim-airline-themes {
        let g:airline_theme='papercolor'
        let g:airline_powerline_fonts = 1
        let g:airline#extensions#tabline#enabled = 1
    " }

    " rainbow_parentheses {
        let g:rbpt_colorpairs = [
            \ ['brown',       'RoyalBlue3'],
            \ ['Darkblue',    'SeaGreen3'],
            \ ['darkgray',    'DarkOrchid3'],
            \ ['darkgreen',   'firebrick3'],
            \ ['darkcyan',    'RoyalBlue3'],
            \ ['darkred',     'SeaGreen3'],
            \ ['darkmagenta', 'DarkOrchid3'],
            \ ['brown',       'firebrick3'],
            \ ['gray',        'RoyalBlue3'],
            \ ['black',       'SeaGreen3'],
            \ ['darkmagenta', 'DarkOrchid3'],
            \ ['Darkblue',    'firebrick3'],
            \ ['darkgreen',   'RoyalBlue3'],
            \ ['darkcyan',    'SeaGreen3'],
            \ ['darkred',     'DarkOrchid3'],
            \ ['red',         'firebrick3'],
        \ ]
        let g:rbpt_max = 16
        au VimEnter * RainbowParenthesesToggle
        au Syntax * RainbowParenthesesLoadRound
        au Syntax * RainbowParenthesesLoadSquare
        au Syntax * RainbowParenthesesLoadBraces
    " }

    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
    " 3 Filesystem Navigation / Movement
    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

    " CtrlP {
        let g:ctrlp_show_hidden = 1
        let g:ctrlp_working_path_mode = 'r' " use the nearest git directory as the cwd:
        let g:ctrlp_custom_ignore = {
            \ 'dir':  '\.git$\|\.hg$\|\.svn$',
            \ 'file': '\.exe$\|\.so$\|\.dll$\|\.pyc$\|\.o$\|\.vcxproj$\|\.d$' }

        if executable('ag')
            let s:ctrlp_fallback = 'ag %s --nocolor -l -g ""'
        elseif executable('ack-grep')
            let s:ctrlp_fallback = 'ack-grep %s --nocolor -f'
        elseif executable('ack')
            let s:ctrlp_fallback = 'ack %s --nocolor -f'
        endif

        " manage buffers more easily with ctrlp
        noremap <leader>bp :CtrlP<cr>
        noremap <leader>bb :CtrlPBuffer<cr>
        noremap <leader>bm :CtrlPMixed<cr>
        noremap <leader>bs :CtrlPMRU<cr>
    " }

    " NERDtree {
        let NERDTreeShowBookmarks=1
        let NERDTreeIgnore=['\.py[cd]$', '\~$', '\.swo$', '\.swp$', '^\.git$', '^\.hg$', '^\.svn$', '\.bzr$']
        let NERDTreeChDirMode=0
        let NERDTreeQuitOnOpen=1
        let NERDTreeMouseMode=2
        let NERDTreeShowHidden=1
        let NERDTreeKeepTreeInNewTab=1
        let NERDTreeMinimalUI = 1
        let NERDTreeDirArrows = 1

        " Add spaces after comment delimiters by default
        let g:NERDSpaceDelims = 1

        " Use compact syntax for prettified multi-line comments
        let g:NERDCompactSexyComs = 1

        noremap <leader>e :NERDTreeToggle<cr>
        noremap <leader>f :e .<cr>
    " }

    " FZF {
        nnoremap <localleader>oh :History:<cr>
        nnoremap <localleader>or :History<cr>
        nnoremap <localleader>of :Files<cr>
        nnoremap <localleader>r  :BTags<cr>
    " }

    " Fswitch {
        " switch between header/source file
        noremap <leader>u :FSHere<cr>
    " }

    " TagBar {
        noremap <F8> :TagbarToggle<cr>
        let g:tagbar_autofocus = 1
    " }

    " Wipeout {
        nnoremap <leader>o :Wipeout<cr>
    " }

    " bufkill {
        nnoremap <leader><leader>q :BD<cr>
    " }

    " EasyMotion {
        nmap <leader>k <Plug>(easymotion-overwin-w)
    " }

    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
    " 4 Editing
    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

    " Tabular {
        vmap <leader>t :Tabularize /
    " }

    " vim-expand-region {
        map U <Plug>(expand_region_expand)
        map Y <Plug>(expand_region_shrink)
    " }

    " Autoformat {
        map <leader>c :Autoformat<cr>
    " }

    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
    " 5 Version Control Integration
    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

    " Fugitive GIT {
        nnoremap <leader>gd :Gdiff<cr>
        nnoremap <leader>gs :Gstatus<cr>
        nnoremap <leader>gb :Gblame<cr>
    " }

    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
    " 6 Snippets
    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

    " Deoplete {
        let g:deoplete#enable_at_startup = 1
        let g:deoplete#sources#clang#libclang_path = "/usr/lib/x86_64-linux-gnu/libclang-4.0.so.1"
        let g:deoplete#sources#clang#clang_header = "/usr/lib/llvm-4.0/lib/clang"

		function! s:check_back_space() abort "{{{
		let col = col('.') - 1
		return !col || getline('.')[col - 1]  =~ '\s'
		endfunction"}}}

		inoremap <silent><expr> <S-TAB>
		\ pumvisible() ? "\<C-p>" :
		\ <SID>check_back_space() ? "\<S-TAB>" :
		\ deoplete#mappings#manual_complete()

		inoremap <silent><expr> <TAB>
		\ pumvisible() ? "\<C-n>" :
		\ <SID>check_back_space() ? "\<TAB>" :
		\ deoplete#mappings#manual_complete()


        inoremap <expr><C-h> deoplete#mappings#manual_complete()."\<C-h>"
        " <C-h>, <BS>: close popup and delete backword char.
        inoremap <expr><BS> deoplete#smart_close_popup()."\<BS>"
    " }

    " UltiSnips {
        " Trigger configuration. Do not use <tab> if you use https://github.com/Valloric/YouCompleteMe.
        let g:UltiSnipsExpandTrigger="<c-l>"
        let g:UltiSnipsJumpForwardTrigger="<c-j>"
        let g:UltiSnipsJumpBackwardTrigger="<c-k>"
    " }

    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
    " 7 Neovim
    """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" }

" Functions {

    " Initialize directories {
    function! InitializeDirectories()
        let parent = $HOME
        let prefix = '/Development/configs/vim/cache/'
        let dir_list = {
                    \ 'backup': 'backupdir',
                    \ 'views': 'viewdir',
                    \ 'swap': 'directory'
        \ }

        if has('persistent_undo')
            let dir_list['undo'] = 'undodir'
        endif

        let common_dir = parent . prefix

        for [dirname, settingname] in items(dir_list)
            let directory = common_dir . dirname
            if exists("*mkdir")
                if !isdirectory(directory)
                    call mkdir(directory)
                endif
            endif
            if !isdirectory(directory)
                echo "Warning: Unable to create backup directory: " . directory
                echo "Try: mkdir -p " . directory
            else
                exec "set " . settingname . "=" . directory . "//"
            endif
        endfor
    endfunction
    call InitializeDirectories()
    " }

    " Strip whitespace {
    function! StripTrailingWhitespace()
        " Preparation: save last search, and cursor position.
        let _s=@/
        let l = line(".")
        let c = col(".")
        " do the business:
        %s/\s\+$//e
        " clean up: restore previous search history, and cursor position
        let @/=_s
        call cursor(l, c)
    endfunction
    " }

    " Shell command {
    function! s:RunShellCommand(cmdline)
        botright new

        setlocal buftype=nofile
        setlocal bufhidden=delete
        setlocal nobuflisted
        setlocal noswapfile
        setlocal nowrap
        setlocal filetype=shell
        setlocal syntax=shell

        call setline(1, a:cmdline)
        call setline(2, substitute(a:cmdline, '.', '=', 'g'))
        execute 'silent $read !' . escape(a:cmdline, '%#')
        setlocal nomodifiable
        1
    endfunction

    command! -complete=file -nargs=+ Shell call s:RunShellCommand(<q-args>)
    " e.g. Grep current file for <search_term>: Shell grep -Hn <search_term> %
    " }

    function! s:ExpandFilenameAndExecute(command, file)
        execute a:command . " " . expand(a:file, ":p")
    endfunction

" }

" vim: set sw=4 ts=4 sts=4 et tw=78 foldenable foldmarker={,} foldmethod=marker:
